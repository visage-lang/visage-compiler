package fxpad;

import javafx.scene.*;
import javafx.scene.effect.*;
import javafx.scene.effect.light.*;
import javafx.scene.transform.*;
import javafx.scene.geometry.*;
import javafx.scene.paint.*;
import javafx.scene.text.*;
import javafx.animation.*; 
import javafx.input.*;

var lighting =
    Lighting {
        light: PointLight {
            x: 43
            y: 0
            z: 183
        }
        surfaceScale: 6.7
        diffuseConstant: .85
        specularConstant: 1.3
    };
    
    

Group {

    var x = 0;
    var y = 0;
    var lx = 0;
    var ly = 0;
    var alpha = 1.0;
    var fadeIn = Timeline {
        keyFrames: [
        KeyFrame { time: 0s  values: alpha => 0.0 },
            KeyFrame { time: 1s  values: alpha => 1.0 tween Interpolator.EASEBOTH }
        ]
    };

    transform: bind Transform.translate(x, y)
    opacity: bind alpha

    onMousePressed: function(e:MouseEvent):Void {
        lx = e.getScreenX().intValue();
        ly = e.getScreenY().intValue();
    }
    onMouseDragged: function(e:MouseEvent):Void {
        // updating x and y will move the group
        // since a translation transform is bound to them
        var deltaX:Integer = e.getScreenX().intValue()- lx;
        var deltaY:Integer = e.getScreenY().intValue() - ly;
        x = x + deltaX;
        y = y + deltaY;
        lx = e.getScreenX().intValue();
        ly = e.getScreenY().intValue();
    }

    onMouseClicked: function(e):Void {
        // updating alpha will fade the group
        // since the opacity of the group is bound to it
        if (alpha == 1) {
            fadeIn.start();
        }
    }


    content:
    [Rectangle {
        cursor: Cursor.HAND
        //selectable: true
        x: 10
        y: 10
        width: 385
        height: 220
        fill: LinearGradient {
            stops:
            [Stop {offset: 0, color: Color.GRAY},
            Stop {offset: .5, color: Color.color(.8, .8, .8, 1)},
            Stop {offset: 1, color: Color.GRAY}]
        }
        stroke: Color.BLACK
        strokeWidth: 1
    },
    Group {
        content: [
        Group {
            effect: DropShadow { offsetX: 7 offsetY: 6 radius: 5 color: Color.color(0, 0, 0, 0.7) }

            content: [
            Path {
                elements: [
                MoveTo {
                    x: 50.0
                    y: 90.0
                    absolute: true
                },
                CurveTo {
                    controlX1: 0.0
                    controlY1: 90.0
                    controlX2: 0.0
                    controlY2: 30.0
                    x: 50.0
                    y: 30.0
                    //smooth: false
                    absolute: true
                },
                LineTo {
                    x: 150.0
                    y: 30.0
                    absolute: true
                },
                CurveTo {
                    controlX1: 200.0
                    controlY1: 30.0
                    controlX2: 200.0
                    controlY2: 90.0
                    x: 150.0
                    y: 90.0
                    //smooth: false
                    absolute: true
                },
                ClosePath {},
                ]
                stroke: Color.RED
                strokeWidth: 10.0
                scaleX: 2
                scaleY: 2
                effect: lighting
            },
            Path {
                elements: [
                MoveTo {
                    x: 60.0
                    y: 80.0
                    absolute: true
                },
                CurveTo {
                    controlX1: 30.0
                    controlY1: 80.0
                    controlX2: 30.0
                    controlY2: 40.0
                    x: 60.0
                    y: 40.0
                    //smooth: false
                    absolute: true
                },
                LineTo {
                    x: 140.0
                    y: 40.0
                    absolute: true
                },
                CurveTo {
                    controlX1: 170.0
                    controlY1: 40.0
                    controlX2: 170.0
                    controlY2: 80.0
                    x: 140.0
                    y: 80.0
                    //smooth: false
                    absolute: true
                },
                ClosePath {},
                ]
                fill: Color.RED
                scaleX: 2
                scaleY: 2
                effect: lighting
            },
            Group {
                content: [
                Text {
                    //verticalAlignment: BASELINE
                    content: "JavaFX"
                    font: Font {name: "Verdana", style: FontStyle.BOLD, size: 25}
                    fill: Color.WHITE
                    x: 50.0
                    y: 69.0
                    scaleX: 2
                    scaleY: 2
                    effect: DropShadow { offsetX: 2 offsetY: 2 radius: 4 color: Color.color(0, 0, 0, 0.8) }
                }]
            }]
        }]
    }]
}

